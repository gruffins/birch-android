plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'jacoco'
}

android {
    namespace 'com.gruffins.birch'
    compileSdk 30

    defaultConfig {
        minSdkVersion 14
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            enableUnitTestCoverage true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues true
            all {
                jacoco {
                    includeNoLocationClasses true
                    excludes = ['jdk.internal.*']
                }
            }
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.2'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = rootProject.properties['com.gruffins.birch.groupId']
            artifactId = project.name
            version = rootProject.properties['com.gruffins.birch.version']

            afterEvaluate {
                from components.release
            }
        }
    }
}

rootCoverage {
    buildVariant 'debug'
    generateCsv false
    generateHtml false
    generateXml true
    executeUnitTests true
    includeUnitTestResults true
}